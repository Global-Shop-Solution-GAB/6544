Program.Sub.ScreenSU.Start
Gui.Form..Create(BaseForm)
Gui.Form..Caption("Incident Maintenance")
Gui.Form..Size(615,659)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..AlwaysOnTop(False)
Gui.Form..FontName("Tahoma")
Gui.Form..FontSize(8.25)
Gui.Form..ControlBox(True)
Gui.Form..MaxButton(True)
Gui.Form..MinButton(True)
Gui.Form..MousePointer(0)
Gui.Form..Moveable(True)
Gui.Form..Sizeable(True)
Gui.Form..ShowInTaskBar(True)
Gui.Form..TitleBar(True)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form.frame1.Create(Frame)
Gui.Form.frame1.Enabled(True)
Gui.Form.frame1.Visible(True)
Gui.Form.frame1.Zorder(0)
Gui.Form.frame1.Size(605,35)
Gui.Form.frame1.Position(4,4)
Gui.Form.frame1.Caption("")
Gui.Form.frame1.FontName("Tahoma")
Gui.Form.frame1.FontSize(8.25)
Gui.Form.lbl1.Create(Label,"Generic Reason",True,75,13,0,5,12,True,0,"Tahoma",8.25,,0,0)
Gui.Form.lbl1.BorderStyle(0)
Gui.Form.lbl1.Parent("frame1")
Gui.Form.cbo1.Create(ComboBox)
Gui.Form.cbo1.Enabled(True)
Gui.Form.cbo1.Visible(True)
Gui.Form.cbo1.Zorder(0)
Gui.Form.cbo1.Size(146,20)
Gui.Form.cbo1.Position(84,8)
Gui.Form.cbo1.FontName("Tahoma")
Gui.Form.cbo1.FontSize(8.25)
Gui.Form.cbo1.Parent("frame1")
Gui.Form.lbl2.Create(Label,"Specific Reason",True,75,13,0,235,12,True,0,"Tahoma",8.25,,0,0)
Gui.Form.lbl2.BorderStyle(0)
Gui.Form.lbl2.Parent("frame1")
Gui.Form.txt1.Create(TextBox,"",True,173,20,0,315,8,True,0,"Tahoma",8.25,,1)
Gui.Form.txt1.Parent("frame1")
Gui.Form.cmdAdd.Create(Button)
Gui.Form.cmdAdd.Enabled(True)
Gui.Form.cmdAdd.Visible(True)
Gui.Form.cmdAdd.Zorder(0)
Gui.Form.cmdAdd.Size(38,23)
Gui.Form.cmdAdd.Position(503,8)
Gui.Form.cmdAdd.Caption("Add")
Gui.Form.cmdAdd.FontName("Tahoma")
Gui.Form.cmdAdd.FontSize(8.25)
Gui.Form.cmdAdd.Parent("frame1")
Gui.Form.cmdAdd.Event(Click,cmdAdd_Click)
Gui.Form.cmdSave.Create(Button)
Gui.Form.cmdSave.Enabled(True)
Gui.Form.cmdSave.Visible(True)
Gui.Form.cmdSave.Zorder(0)
Gui.Form.cmdSave.Size(52,23)
Gui.Form.cmdSave.Position(545,7)
Gui.Form.cmdSave.Caption("Save")
Gui.Form.cmdSave.FontName("Tahoma")
Gui.Form.cmdSave.FontSize(8.25)
Gui.Form.cmdSave.Parent("frame1")
Gui.Form.cmdSave.Anchor(9)
Gui.Form.cmdSave.Event(Click,cmdSave_Click)
Gui.Form.GsGcDisplay.Create(GsGridControl)
Gui.Form.GsGcDisplay.Enabled(True)
Gui.Form.GsGcDisplay.Visible(True)
Gui.Form.GsGcDisplay.Zorder(0)
Gui.Form.GsGcDisplay.Size(604,586)
Gui.Form.GsGcDisplay.Position(5,41)
Gui.Form.GsGcDisplay.Anchor(15)
Gui.Form.GsGcDisplay.Event(RowCellClick,GsGcDisplay_RowCellClick)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
v.Global.bChanged.Declare(Boolean, False)
Program.Sub.Preflight.End

Program.Sub.Main.Start
Function.Intrinsic.UI.UsePixels ' Allows you to use Pixels instead of Twips throughout
f.Intrinsic.Control.CallSub(createDisplayDT)
f.Intrinsic.Control.CallSub(load)
f.Intrinsic.Control.CallSub(loadGridProperties)
gui.Form..Show
Program.Sub.Main.End

Program.Sub.createDisplayDT.Start
f.Intrinsic.Control.Try
	f.Data.DataTable.Create("displayDT", true)
	f.Data.DataTable.AddColumn("displayDT", "Delete", "String", "Delete")
	f.Data.Datatable.AddColumn("displayDT", "IncidentType", "String")
	f.Data.Datatable.AddColumn("displayDT", "Incident", "String")
	
	f.Data.DataTable.Create("saveDT", true)
	f.Data.Datatable.AddColumn("saveDT", "IncidentType", "String")
	f.Data.Datatable.AddColumn("saveDT", "Incident", "String")
	
	f.Data.DataView.Create("displayDT", "displayDV", 22)
	gui.Form.GsGcDisplay.AddGridviewFromDataview("displayGV", "displayDT", "displayDV")
	gui.Form.GsGcDisplay.MainView("displayGV")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createDisplayDT.End

Program.Sub.loadGridProperties.Start
v.Local.iCnt.Declare
v.Local.sFields.Declare
v.Local.iVisibleIndex.Declare

f.Intrinsic.Control.Try
	f.Intrinsic.String.Split(v.DataTable.displayDT.FieldNames, "*!*", v.Local.sFields)
	f.Intrinsic.Control.For(v.Local.iCnt, 0, v.Local.sFields.UBound, 1)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderFontBold", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "Visible", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "ReadOnly", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "AllowEdit", False)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "CellHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderBackColor", v.Color.GSS)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderForeColor", v.Color.White)
	f.Intrinsic.Control.Next(v.Local.iCnt)
	
	'GridView Properties
	gui.Form.GsGcDisplay.BestFitColumns("displayGV")
	gui.Form.GsGcDisplay.SetGridviewProperty("displayGV", "OptionsViewColumnAutoWidth", False)
	'gui.Form.GsGcDisplay.SetGridviewProperty("displayGV", "OptionsViewShowAutoFilterRow", True)
	'gui.Form.GsGcDisplay.SetGridviewProperty("displayGV", "OptionsFindAlwaysVisible", True)
	'gui.Form.GsGcDisplay.SetGridviewProperty("displayGV", "Showgrouppanel", True)
	
	'Possible Column Properties 
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "ReadOnly", False)
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "AllowEdit", True)
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "HeaderHAlignment", "Center")
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "IncidentType", "CellHAlignment", "Center")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "IncidentType", "Caption", "Generic Reason")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Incident", "Caption", "Specific Reason")
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "Visible", False)
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "DisplayCustomDatetime", "d")
	
	'VisibleIndex
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "VisibleIndex", v.Local.iVisibleIndex)
	'gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "VisibleIndex", v.Local.iVisibleIndex.++)
	
	'Conditional Formatting
	'gui.Form.GsGcDisplay.AddStyleFormatCondition("displayGV", "columnName", "TagName", "EQUAL", 1)
	'gui.Form.GsGcDisplay.SetStyleFormatConditionProperty("displayGV", "columnName", "TagName", "ApplyToRow", True) 
	'gui.Form.GsGcDisplay.SetStyleFormatConditionProperty("displayGV", "columnName", "TagName", "BackColor", "Color") 
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.loadGridProperties.End

Program.Sub.load.Start
f.Intrinsic.Control.Try
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.Datatable.CreateFromSQL("tmpDT", "con", "Select * from GCG_6544_INCIDENT order by IncidentType, Incident asc")
		f.Data.Datatable.CreateFromSQL("cboDT", "con", "Select distinct IncidentType from GCG_6544_INCIDENT")
		gui.Form.cbo1.AddItems("DataTable", "cboDT", "IncidentType", "IncidentType")
		f.Data.Datatable.Close("cboDT")
	f.ODBC.Connection!con.Close
	'Merge into Display
	f.Data.Datatable.DeleteRow("displayDT")
	f.Data.Datatable.AcceptChanges("displayDT")
	f.Data.Datatable.Merge("tmpDT", "displayDT", false, 2)
	f.Data.Datatable.Close("tmpDT")
	f.Data.Datatable.AcceptChanges("displayDT")	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.load.End

Program.Sub.save.Start
f.Intrinsic.Control.Try
	'Merge into Save
	f.Data.Datatable.AcceptChanges("displayDT")
	f.Data.Datatable.DeleteRow("saveDT")
	f.Data.Datatable.AcceptChanges("saveDT")
	f.Data.Datatable.Merge("displayDT", "saveDT", false, 2)
	f.Data.Datatable.AcceptChanges("saveDT")
	
	f.ODBC.Connection!con.OpenCompanyConnection
		f.ODBC.Connection!con.Execute("Delete from GCG_6544_INCIDENT")
		f.Data.Datatable.SaveToDB("saveDT", "con", "GCG_6544_INCIDENT", "IncidentType*!*Incident", 256)
	f.ODBC.Connection!con.Close
	'Reset changed.
	v.Global.bChanged.Set(False)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.save.End

Program.Sub.Form_UnLoad.Start
v.Local.iRet.Declare
f.Intrinsic.Control.Try
	'Check to Save.
	f.Intrinsic.Control.If(v.Global.bChanged)
		f.Intrinsic.UI.Msgbox("Do you wish to save changes?", "Save?", 4, v.Local.iRet)
		f.Intrinsic.Control.If(v.Local.iRet, =, 6)
			f.Intrinsic.Control.CallSub("Save")
		f.Intrinsic.Control.EndIf
	f.Intrinsic.Control.EndIf
	
	'End Program.
	f.Intrinsic.Control.End
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Form_UnLoad.End

Program.Sub.cmdAdd_Click.Start
v.Local.sType.Declare
v.Local.sIncident.Declare
f.Intrinsic.Control.Try
	'Check is not empty.
	v.Local.sType.Set(v.Screen.Form!cbo1.Text)
	f.Intrinsic.Control.If(v.Local.sType.Trim, =, "")
		f.Intrinsic.UI.Msgbox("Please select an Incident Type.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	v.Local.sIncident.Set(v.Screen.Form!txt1.Text)
	f.Intrinsic.Control.If(v.Local.sIncident.Trim, =, "")
		f.Intrinsic.UI.Msgbox("Please select an Incident.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	'Set as changed.
	v.Global.bChanged.Set(True)
	'Add to display
	f.Data.Datatable.AddRow("displayDT", "IncidentType", v.Local.sType, "Incident", v.Local.sIncident)
	f.Data.Datatable.AcceptChanges("displayDT")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.cmdSave_Click.Start
f.Intrinsic.Control.Try
	'Save
	f.Intrinsic.Control.CallSub("save")
	'Reload to remove duplicates in the screen.
	f.Intrinsic.Control.CallSub("load")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.GsGcDisplay_RowCellClick.Start
f.Intrinsic.Control.Try
	'Select Case for delete or load text
	f.Intrinsic.Control.SelectCase(v.Args.column)
		f.Intrinsic.Control.Case("Delete")
			'Set as changed.
			v.Global.bChanged.Set(True)
			'Delete Row
			f.Data.Datatable.DeleteRow("displayDT", v.Args.RowIndex)
			f.Data.Datatable.AcceptChanges("displayDT")
		f.Intrinsic.Control.CaseElse
			gui.Form.cbo1.Text(v.Datatable.displayDT(v.Args.RowIndex).IncidentType!FieldValTrim)
			gui.Form.txt1.Text(v.Datatable.displayDT(v.Args.RowIndex).Incident!FieldValTrim)
	f.Intrinsic.Control.EndSelect
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.GsGcDisplay_RowCellClick.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Generic Error message.
f.Intrinsic.String.Build("Project GCG_6544_Incident_Maintenance.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.Comments.Start
${$5$}$3.0.0.0$}$1
${$6$}$aanazco$}$20211129174356914$}$7aQAV3Tor/tR9bkuakHbuzQlHVuXCGOzBvRGw+CKLWneOvPWwOEjAF2Yn+M2IGkbru8cFHcRBFo=
Program.Sub.Comments.End